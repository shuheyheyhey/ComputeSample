// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel Main

#include "../Utils/ComputeShaderHelper.hlsl"

// 入力される頂点情報
struct SourceVertex
{
    float3 positionOS;
    float2 uv;
};

// 描画先の頂点情報
struct DrawVertex
{
    float3 positionWS;
    float2 uv;
};

// 三角形の情報
struct DrawTriangle
{
    float3 normalWS;
    DrawVertex vertices[3];
};

// C#から情報を渡すための Buffer を定義
// メッシュから入力される頂点情報
StructuredBuffer<SourceVertex> _SourceVertices;
// メッシュの triangles の情報　
StructuredBuffer<int> _SourceTriangles;
// 入力された情報から得た出力する三角形の情報
AppendStructuredBuffer<DrawTriangle> _DrawTriangles;

// 出力される三角形の数(C# でメッシュから計算する)
int _NumSourceTriangles;
// 各三角形が法線方向に伸びる高さ
float _PyramidHeight;
// Unity のWorld座標
float4x4 _LocalToWorld;

// 与えられた頂点情報を Unity の World 座標に変換する
DrawVertex TransformToWorldSpace(SourceVertex v)
{
    DrawVertex o;
    o.positionWS = mul(_LocalToWorld, float4(v.positionOS, 1)).xyz;
    o.uv = v.uv;
    return o;
}

// 与えられた頂点情報から三角形の情報として出力バッファーに適用
void SetupAndOutputTriangle(DrawVertex a, DrawVertex b, DrawVertex c)
{
    float3 normalWS = GetNormalFromTriangle(a.positionWS, b.positionWS, c.positionWS);

    DrawTriangle tri;
    tri.normalWS = normalWS;
    tri.vertices[0] = a;
    tri.vertices[1] = b;
    tri.vertices[2] = c;

    _DrawTriangles.Append(tri);
}

[numthreads(128, 1, 1)]
void Main(uint3 id: SV_DispatchThreadID)
{
    if ((int)id.x >= _NumSourceTriangles) { return; }

    int triStart = id.x * 3;
    DrawVertex inputs[3];
    inputs[0] = TransformToWorldSpace(_SourceVertices[_SourceTriangles[triStart]]);
    inputs[1] = TransformToWorldSpace(_SourceVertices[_SourceTriangles[triStart + 1]]);
    inputs[2] = TransformToWorldSpace(_SourceVertices[_SourceTriangles[triStart + 2]]);

    DrawVertex center;
    float3 triNormal = GetNormalFromTriangle(inputs[0].positionWS, inputs[1].positionWS, inputs[2].positionWS);
    center.positionWS = GetTriangleCenter(inputs[0].positionWS, inputs[1].positionWS, inputs[2].positionWS) + triNormal * _PyramidHeight;
    center.uv = GetTriangleCenter(inputs[0].uv, inputs[1].uv, inputs[2].uv);

    SetupAndOutputTriangle(inputs[0], inputs[1], center);
    SetupAndOutputTriangle(inputs[1], inputs[2], center);
    SetupAndOutputTriangle(inputs[2], inputs[0], center);
}

